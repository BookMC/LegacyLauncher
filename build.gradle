plugins {
    id "java"
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

configurations {
    include
    implementation.extendsFrom(include)
}

repositories {
    maven {
        url "https://libraries.minecraft.net/"
    }
    mavenCentral()
}

group = "net.minecraft"
archivesBaseName = "launchwrapper"
version = "1.2.1"

sourceCompatibility = targetCompatibility = 1.8

dependencies {
    implementation "net.sf.jopt-simple:jopt-simple:4.5"

    include "org.ow2.asm:asm:9.2"
    include "org.ow2.asm:asm-tree:9.2"

    implementation "org.lwjgl.lwjgl:lwjgl:2.9.1"
    implementation "org.apache.logging.log4j:log4j-core:2.0-beta9"
    implementation "org.apache.logging.log4j:log4j-api:2.0-beta9"
}

shadowJar {
    configurations = [project.configurations.include]
    archiveClassifier.set("")
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
}

tasks.build.dependsOn(tasks.shadowJar)

publishing {
    repositories {
        maven {
            url = "$buildDir/repository"
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = "LegacyLauncher"
                url = "https://github.com/BookMC/LegacyLauncher"

                developers {
                    developer {
                        name = "Mojang"
                    }
                }
            }
        }
    }
}